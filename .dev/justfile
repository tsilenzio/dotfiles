# Development/testing utilities (internal use)
# Usage: just dev <recipe>

set shell := ["bash", "-cu"]

DEV_DIR := justfile_directory()
SCRIPTS_DIR := DEV_DIR / "scripts"
DOTFILES_DIR := parent_directory(DEV_DIR)

# Show dev commands
default:
    @just --justfile "{{justfile()}}" --list

# Install CLT and Homebrew from .cache/ (offline installers)
setup:
    "{{SCRIPTS_DIR}}/setup"

# Pre-fetch Homebrew packages: test (default), work, personal, all
prefetch *args:
    "{{SCRIPTS_DIR}}/prefetch" {{args}}

# Test bootstrap: local (default) or curl (emulates curl|bash)
bootstrap *args:
    "{{SCRIPTS_DIR}}/test" {{args}}

# Run install with test bundle visible
install:
    "{{DOTFILES_DIR}}/install.sh" --reveal test

# Run linters (shellcheck + zsh syntax)
lint:
    "{{SCRIPTS_DIR}}/lint"

# Lock brew operations (skip installs during upgrade, show diffs instead)
lock:
    @mkdir -p "{{DOTFILES_DIR}}/.state"
    @touch "{{DOTFILES_DIR}}/.state/brew.lock"
    @echo "Brew locked. Upgrades will skip package installation."
    @echo "Unlock with: rune dev unlock"

# Unlock brew operations
unlock:
    @rm -f "{{DOTFILES_DIR}}/.state/brew.lock"
    @echo "Brew unlocked. Upgrades will install packages normally."

# Preview a PR, branch, or commit (snapshot + checkout)
preview *args:
    "{{SCRIPTS_DIR}}/preview" {{args}}

# Show dev lock status
status:
    @echo "Brew: $(test -f '{{DOTFILES_DIR}}/.state/brew.lock' && echo 'locked' || echo 'unlocked')"
