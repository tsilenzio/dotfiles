# Development/testing utilities (internal use)
# Usage: just dev <recipe>

set shell := ["bash", "-cu"]

DEV_DIR := justfile_directory()
DOTFILES_DIR := parent_directory(DEV_DIR)

# Show dev commands
default:
    @just --justfile "{{justfile()}}" --list

# Install CLT and Homebrew from .cache/ (offline installers)
setup:
    "{{DEV_DIR}}/setup.sh"

# Pre-fetch Homebrew packages: test (default), work, personal, all
prefetch *args:
    "{{DEV_DIR}}/prefetch.sh" {{args}}

# Run bootstrap: local (default) or curl (emulates curl|bash)
bootstrap mode="local" *args:
    #!/usr/bin/env bash
    set -e
    cd "{{DOTFILES_DIR}}"
    if [[ "{{mode}}" == "curl" ]]; then
        echo "Emulating: curl ... | bash -s -- --reveal test {{args}}"
        cat bootstrap.sh | bash -s -- --reveal test {{args}}
    else
        ./bootstrap.sh --reveal test {{args}}
    fi

# Run install with test bundle visible
install:
    "{{DOTFILES_DIR}}/install.sh" --reveal test

# Run linters (shellcheck + zsh syntax)
lint:
    #!/usr/bin/env bash
    set -e
    echo "Running ShellCheck..."
    find "{{DOTFILES_DIR}}" -type f \( -name "*.sh" -o -name "*.bash" \) \
        ! -path "*/.git/*" ! -path "*/.cache/*" \
        -exec shellcheck --severity=warning -x {} +
    echo ""
    echo "Checking Zsh syntax..."
    find "{{DOTFILES_DIR}}" -type f \( -name "zshrc*" -o -name "zshenv*" -o -name "*.zsh" \) \
        ! -path "*/.git/*" ! -path "*/.cache/*" \
        -exec zsh -n {} \;
    echo ""
    echo "All checks passed!"
